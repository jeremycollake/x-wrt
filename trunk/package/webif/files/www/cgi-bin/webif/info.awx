#!/usr/bin/awx
BEGIN {
	CATEGORY="Info"
	include("/usr/lib/webif/common.awx")
	getline < "/www/.version"
	rev=$0
}

function update_check() {
	if (getvar("check_daily") != "") {
		versionfile="/.version"
		daily_checked=1
	} else {
		versionfile="/.version-stable"
	}

	print "@TR<<Please wait>> ...<br />"
	"wget -O- -q "version_url versionfile" 2>&1" | getline
	newrev=$0
	if ((newrev ~ /doesn't exist/) || (newrev ~ /not found/)) {
		upgrade_error = 1
	} else {
		if (!(int(newrev) > int(rev))) {
			newrev=""
			no_new_ver = 1
		}
	}
}

function do_update() {
	package_filename="webif_latest.ipk"
	print "@TR<<info_wait_install#Please wait, installation may take a minute>> ... <br />"
	print "<pre>"
	system("ipkg -V 0 update")
	system("ipkg install \""version_url "/" package_filename"\" -force-overwrite -force-reinstall| uniq")
	print "</pre>"

	getline < "/www/.version"
	this_rev=$0
	lang=config_get("general", "lang")
	"ipkg status \"webif-lang-"lang"\" 2>/dev/null |grep \"Status:\" | grep \" installed\"" | getline
	if ($0 != "") {
		webif_version="r" this_rev
		while (("ipkg status webif" | getline) == 1) {
			if (/^Version:/) {
				webif_version=$2
				gsub(/ /,"",webif_version)
			}
		}
		if (lang != "en") {
			print "<pre>"
			system("ipkg install \""version_url"/packages/webif-lang-"lang"_"webif_version"_all.ipk\" -force-reinstall -force-overwrite | uniq")
			print "</pre>"
		}
	}
}

function do_action() {
	if (repo && (getvar("update_check") != "")) update_check()
	else show_rev = rev
	if (getvar("install_webif") != "") do_update()	
}

function handle_default() {
	"uname -srv" | getline
	kver=$0
	"/sbin/ifconfig eth0 | grep HWaddr | cut -b39-" | getline
	mac=$0
	firmwarestr = config_get("general", "firmware_name") " - " config_get("general", "firmware_subtitle") " " config_get("general", "firmware_version")
	devname = config_get("general", "device_name")
	devname = ( devname ? devname : "unidentified" )
	"cat /proc/cpuinfo | sed 2,20d | cut -c16-" | getline
	if ($0 == "") "uname -m" | getline
	boardtype = $0
	username = ENVIRON["REMOTE_USER"]
	"uname -a" | getline
	machinfo = $0
	if (machinfo ~ /mips/) {
		if (boardtype ~ /Atheros/) repo = "atheros-2.6"
		else if (machinfo ~ /2\.4/) repo = "brcm-2.4"
		else if (machinfo ~ /2\.6/) repo = "brcm-2.6"
	} else if (machinfo ~ / i[0-9]86 /) repo = "x86-2.6"
	else {
		"cat /proc/cpuinfo" | getline
		if ($0 ~ /IXP4/) repo = "ixp4xx-2.6"
	}
	revision = config_get("general", "firmware_version")
	if (revision == "7.06") {
		version_url = "http://downloads.x-wrt.org/xwrt/kamikaze/" revision "/" repo;
	} else {
		version_url = "http://downloads.x-wrt.org/xwrt/kamikaze/snapshots/" repo;
	}
	PAGENAME = "System"
	page_title = "<img src=\"/images/blkbox.jpg\" alt=\"@TR<<System Information>>\"/>@TR<<System Information>>"
	RENDER = "views/info-system.ahtml"
}

function handle_aboutinfo() {
	PAGENAME = "About"
	page_title = "<img src=\"/images/abt.jpg\" alt=\"@TR<<About>>\" />@TR<<About>>"
	RENDER = "views/info-about.ahtml"
}

##WEBIF:name:Info:001:System
##WEBIF:name:Info:950:About:aboutinfo
